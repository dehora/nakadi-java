

dependencies {
  implementation project(':nakadi-java-client')
  implementation project.libs.guava
  implementation project.libs.gson
  implementation project.libs.slf4j

  testImplementation project.libs.logback_core
  testImplementation project.libs.logback_classic
  testImplementation project.libs.junit
}

sourceSets {
  main {
    java {
      srcDir 'src/main/resources'
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {

      artifact(jar) {
      }

      artifact(sourceJar) {
      }

      artifact(javadocJar) {
      }

      pom.withXml {

        asNode().with {
          appendNode('url', 'https://github.com/dehora/nakadi-java')
          appendNode('description', 'Client driver gson support')
          appendNode('scm').with {
            appendNode('url', 'git@github.com:dehora/nakadi-java.git')
          }
          appendNode('licenses').with {
            appendNode('license').with {
              appendNode('name', 'MIT License')
            }
          }
        }

        def dependenciesNode = asNode().appendNode('dependencies')
        configurations.implementation.allDependencies.each {
          def dependencyNode = dependenciesNode.appendNode('dependency')
          dependencyNode.appendNode('groupId', it.group)
          dependencyNode.appendNode('artifactId', it.name)
          dependencyNode.appendNode('version', it.version)
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

